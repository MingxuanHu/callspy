apply plugin: 'java'

group = "com.zeroturnaround"
jar.baseName = 'callspy'
version = 0.1

repositories {
    jcenter()
}

dependencies {
    compile 'net.bytebuddy:byte-buddy:1.4.25'
    testCompile "junit:junit:4.11"
}

compileJava {
    options.debug = true
}

jar {
    archiveName = "${rootProject.name}-${rootProject.version}.jar"

    //build fat jar
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }}

    manifest {
        attributes(
                'Main-Class': 'org.example.callspy.example.RandomClassCallingThings',
                'Premain-Class': 'com.zeroturnaround.callspy.Agent',
                'Agent-Class': 'com.zeroturnaround.callspy.Agent',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true',
                'Can-Set-Native-Method-Prefix': 'true',
                'Implementation-Title': "CallSpy",
                'Implementation-Version': rootProject.version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version')
        )
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}